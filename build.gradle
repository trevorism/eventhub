buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url uri("https://trevorism-build.eastus.cloudapp.azure.com/nexus/repository/maven-releases")
        }
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.1.0'
        classpath 'com.trevorism:gradle-acceptance-plugin:2.1.0'
        classpath 'com.trevorism:gradle-gae-deploy-plugin:1.3.0'
        classpath 'com.trevorism:gradle-release-plugin:0.5.0'
    }
}

plugins {
    id "groovy"
    id "war"
    id "jacoco"
}

apply plugin: 'com.google.cloud.tools.appengine'
apply plugin: "com.trevorism.gradle.acceptance"
apply plugin: "com.trevorism.gradle.gae.deploy"
apply plugin: "com.trevorism.gradle.release"

repositories {
    mavenCentral()
    maven {
        url uri("https://trevorism-build.eastus.cloudapp.azure.com/nexus/repository/maven-releases")
    }
}

def jerseyVersion = "2.35"

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.5.15'

    implementation 'com.google.cloud:google-cloud-pubsub:1.115.1'
    implementation 'com.google.auth:google-auth-library-oauth2-http:1.4.0'

    implementation 'com.trevorism:secure-http-utils:0.7.0'
    implementation 'com.google.code.gson:gson:2.8.9'

    implementation "org.glassfish.jersey.containers:jersey-container-servlet:$jerseyVersion"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVersion"
    implementation "org.glassfish.jersey.inject:jersey-hk2:$jerseyVersion"
    implementation 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.13.1'

    implementation 'io.swagger:swagger-jersey2-jaxrs:1.6.4'
    implementation 'org.slf4j:slf4j-nop:1.7.35'

    acceptanceImplementation 'io.cucumber:cucumber-groovy:4.7.1'
    acceptanceImplementation 'com.trevorism:datastore-client:2.4.0'
    acceptanceImplementation 'commons-beanutils:commons-beanutils:1.9.4'

    testImplementation 'junit:junit:4.13.2'
}

appengine {
    deploy {
        projectId = "trevorism-eventhub"
        version = "1-7-0"
        stopPreviousVersion = true
        promote = true
    }
}

check {
    dependsOn('jacocoTestReport')
}

task deliver {
    dependsOn("build", "appengineDeploy", "acceptance")
    appengineDeploy.mustRunAfter("check")
}

processResources{
    doLast{
        copy{
            from "${System.getProperty("user.home")}/${project.name}/secrets.properties"
            into "build/resources/main"
        }
    }
}

acceptanceSettings {
    stepDefinitionPackageRoot = "com/trevorism/eventhub" //default is com/trevorism/gcloud
}
